plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

description = 'Library for authentication and authorization with the RADAR platform using Jersey'
group = 'org.radarbase'
version = '0.1.0'

ext {
    githubRepoName = 'RADAR-base/ManagementPortal'
    githubUrl = 'https://github.com/RADAR-base/ManagementPortal'
    issueUrl = "https://github.com/$githubRepoName/issues".toString()
    website = 'http://radar-base.org'

    hk2Version = "2.6.1"
    managementPortalVersion = "0.5.7"
    jakartaWsRsVersion = "2.1.6"
    jakartaAnnotationVersion = "1.3.5"
    jerseyVersion = "2.29.1"
    grizzlyVersion = "2.4.4"
    jacksonVersion = "2.9.10"
    okhttpVersion = "4.2.0"
}

repositories {
    jcenter()
    maven { url = "https://dl.bintray.com/radar-cns/org.radarcns" }
    maven { url = "https://repo.thehyve.nl/content/repositories/snapshots" }
}

dependencies {
    api("org.radarcns:radar-auth:$managementPortalVersion")

    api("jakarta.ws.rs:jakarta.ws.rs-api:$jakartaWsRsVersion")
    api("jakarta.annotation:jakarta.annotation-api:$jakartaAnnotationVersion")
    api("org.glassfish.hk2.external:jakarta.inject:$hk2Version")
    api("org.glassfish.jersey.core:jersey-server:$jerseyVersion")

    api("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")

    implementation 'com.github.spullara.mustache.java:compiler:0.9.6'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.slf4j:slf4j-api:1.7.26"

    testImplementation("org.junit.jupiter:junit-jupiter:5.5.2")
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation("com.squareup.okhttp3:okhttp:$okhttpVersion")

    testImplementation("org.glassfish.grizzly:grizzly-http-server:${grizzlyVersion}")

    testImplementation("org.glassfish.jersey.containers:jersey-container-grizzly2-http:${jerseyVersion}")
    testImplementation("org.glassfish.jersey.containers:jersey-container-grizzly2-servlet:${jerseyVersion}")
    testImplementation("org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}")
    testRuntimeOnly("org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}")

    testRuntimeOnly 'javax.xml.bind:jaxb-api:2.2.11'
    testRuntimeOnly 'com.sun.xml.bind:jaxb-core:2.2.11'
    testRuntimeOnly 'com.sun.xml.bind:jaxb-impl:2.2.11'
    testRuntimeOnly 'javax.activation:activation:1.1.1'

    testRuntime 'org.slf4j:slf4j-simple:1.7.26'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.jvmTarget = "11"
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

def sharedManifest = manifest {
    attributes("Implementation-Title": project.name,
            "Implementation-Version": version)
}

jar {
//    archiveBaseName.set(project.name)
    manifest.from sharedManifest
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    manifest.from sharedManifest
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
    manifest.from sharedManifest
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

ext.nexusRepoBase = 'https://repo.thehyve.nl/content/repositories'

publishing {
    publications {
        mavenJar(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.description
                url = githubUrl
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'dennyverbeeck'
                        name = 'Denny Verbeeck'
                        email = 'dverbeec@its.jnj.com'
                        organization = 'Janssen R&D'
                    }
                    developer {
                        id = 'blootsvoets'
                        name = 'Joris Borgdorff'
                        email = 'joris@thehyve.nl'
                        organization = 'The Hyve'
                    }
                    developer {
                        id = 'nivemaham'
                        name = 'Nivethika Mahasivam'
                        email = 'nivethika@thehyve.nl'
                        organization = 'The Hyve'
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = githubUrl + '/issues'
                }
                organization {
                    name = 'RADAR-base'
                    url = website
                }
                scm {
                    connection = 'scm:git:' + githubUrl
                    url = githubUrl
                }
            }

        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "$nexusRepoBase/releases"
            def snapshotsRepoUrl = "$nexusRepoBase/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty('nexusUser') ? project.property('nexusUser') : System.getenv('NEXUS_USER')
                password = project.hasProperty('nexusPassword') ? project.property('nexusPassword') : System.getenv('NEXUS_PASSWORD')
            }
        }
    }
}

bintray {
    user project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    override false
    publications 'mavenJar'
    pkg {
        repo = project.group
        name = project.name
        userOrg = 'radar-base'
        desc = project.description
        licenses = ['Apache-2.0']
        websiteUrl = website
        issueTrackerUrl = issueUrl
        vcsUrl = githubUrl
        githubRepo = githubRepoName
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            desc = project.description
            vcsTag = System.getenv('TRAVIS_TAG')
            released = new Date()
        }
    }
}

bintrayUpload.dependsOn 'assemble'

wrapper {
    gradleVersion = "5.6.2"
}